plugins {
    id "java-library"
    id "signing"
    id "maven-publish"
    id "jacoco"
    id "com.github.hierynomus.license" version "0.16.1"
    id "org.sonarqube" version "3.3"
    id "io.codearte.nexus-staging" version "0.30.0"
}

repositories {
    mavenCentral()
}

group 'com.github.kaklakariada'
version = '1.4.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addBooleanOption('html5', true)
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
    options.encoding = 'UTF-8'
}

test {
    if(logger.infoEnabled) {
        testLogging.showStandardStreams = true
    }
    jvmArgs '-XX:+HeapDumpOnOutOfMemoryError', '-enableassertions'
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.subshell.simpleframework:simple-xml:2.9.0'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    
    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.9.0'
    testImplementation 'org.assertj:assertj-core:3.19.0'
}

license {
    header = file('gradle/license-header.txt')
    exclude('**/deviceList*Payload.xml')
    exclude('devicelist6840.xml')
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

rootProject.tasks["sonarqube"].dependsOn(tasks["jacocoTestReport"])

def getOptionalProperty(String name) {
    if(project.hasProperty(name)) {
        return project.property(name)
    }
    logger.info("Project property '${name}' not available. Please it to ~/.gradle/gradle.properties")
    return null
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'FritzBox Java API'
                description = 'Java API for managing FritzBox HomeAutomation '
                url = 'https://github.com/kaklakariada/fritzbox-java-api'

                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kaklakariada'
                        name = 'Christoph'
                        email = 'kaklakariada@chp1.net'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/kaklakariada/fritzbox-java-api.git'
                    developerConnection = 'scm:git:https://github.com/kaklakariada/fritzbox-java-api.git'
                    url = 'https://github.com/kaklakariada/fritzbox-java-api'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            allowInsecureProtocol = false
            credentials(PasswordCredentials) {
                username = getOptionalProperty("ossrhUsername")
                password = getOptionalProperty("ossrhPassword")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusStaging {
    packageGroup = project.group
    username = getOptionalProperty("ossrhUsername")
    password = getOptionalProperty("ossrhPassword")
}
