plugins {
    id "java"
    id "eclipse"
    id "maven-publish"
    id "com.github.hierynomus.license" version "0.14.0"
    id "com.jfrog.bintray" version "1.8.0"
}

repositories {
    jcenter()
}

group 'com.github.kaklakariada'
version = '0.4.1'
sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
    options.encoding = 'UTF-8'
}

test {
    if(logger.infoEnabled) {
        testLogging.showStandardStreams = true
    }
    jvmArgs '-XX:+HeapDumpOnOutOfMemoryError', '-enableassertions'
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'com.subshell.simpleframework:simple-xml:2.9.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    
    testRuntime 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.11.0'
}

license {
    header = file('gradle/license-header.txt')
    ext.year = 2017
    ext.name = 'Christoph Pirkl'
    ext.email = 'christoph at users.sourceforge.net'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}


bintray {
    def bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    def bintrayApiKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    user = bintrayUser
    key = bintrayApiKey
    publications = ['BintrayPublication']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = bintrayUser
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/kaklakariada/fritzbox-java-api.git'
        version {
            name = project.version
            released  = new Date()
            vcsTag = project.version
        }
    }
}

eclipse {
    classpath {
        downloadSources = true
    }
    jdt.file {
        beforeMerged  { jdt ->
          File defaultProperties = new File("${rootProject.projectDir}/gradle/defaultEclipseJdtPrefs.properties").absoluteFile
          logger.info "Load defaults from $defaultProperties for $project"
          jdt.load(defaultProperties)
        }
    }
}