plugins {
    id "java-library"
    id "maven-publish"
    id "jacoco"
    id "com.github.hierynomus.license" version "0.16.1"
    id "org.sonarqube" version "6.1.0.5360"
    id "org.jreleaser" version "1.18.0"
    id "com.github.ben-manes.versions" version "0.52.0"
    id "org.sonatype.gradle.plugins.scan" version "3.1.0"
}

group = 'com.github.kaklakariada'
version = '1.7.1'

java {
    toolchain {
        def javaVersion = project.hasProperty('javaVersion') ? project.getProperty('javaVersion') : 11
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    failOnError = false
    options.addBooleanOption('html5', true)
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
    options.encoding = 'UTF-8'
}

testing {
    suites { 
        test { 
            useJUnitJupiter() 
        }
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.subshell.simpleframework:simple-xml:2.9.0'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.5.18'
    
    testImplementation 'org.mockito:mockito-core:5.17.0'
    testImplementation 'org.assertj:assertj-core:3.27.3'
}

license {
    header = file('gradle/license-header.txt')
    exclude('**/deviceList*Payload.xml')
    exclude('devicelist6840.xml')
    exclude('**/FritzOS29/*.xml')
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonar {
  properties {
    property("sonar.organization", "kaklakariada-github")
    property("sonar.host.url", "https://sonarcloud.io")
    property("sonar.gradle.skipCompile", "true")
  }
}

rootProject.tasks["sonarqube"].dependsOn(tasks["jacocoTestReport"])

def getOptionalProperty(String name) {
    if(project.hasProperty(name)) {
        return project.property(name)
    }
    logger.info("Project property '${name}' not available. Please add it to ~/.gradle/gradle.properties")
    return null
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'FritzBox Java API'
                description = 'Java API for managing FritzBox HomeAutomation '
                url = 'https://github.com/kaklakariada/fritzbox-java-api'

                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kaklakariada'
                        name = 'Christoph'
                        email = 'kaklakariada@chp1.net'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/kaklakariada/fritzbox-java-api.git'
                    developerConnection = 'scm:git:https://github.com/kaklakariada/fritzbox-java-api.git'
                    url = 'https://github.com/kaklakariada/fritzbox-java-api'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
  signing {
    active = 'ALWAYS'
    armored = true
    verify = true
  }
  deploy {
    maven {
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  gradleReleaseChannel = "current"
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}

ossIndexAudit {
    allConfigurations = false
    useCache = true
    excludeVulnerabilityIds = []
    printBanner = false
}

rootProject.tasks["build"].dependsOn(tasks["ossIndexAudit"])
