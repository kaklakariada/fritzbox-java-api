plugins {
    id "java"
    id "maven-publish"
    id "com.github.hierynomus.license" version "0.15.0"
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    jcenter()
}

group 'com.github.kaklakariada'
version = '1.0.0'
sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
    options.encoding = 'UTF-8'
}

test {
    if(logger.infoEnabled) {
        testLogging.showStandardStreams = true
    }
    jvmArgs '-XX:+HeapDumpOnOutOfMemoryError', '-enableassertions'
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.2.2'
    implementation 'com.subshell.simpleframework:simple-xml:2.9.0'
    implementation 'org.slf4j:slf4j-api:1.7.29'
    
    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.1.0'
}

license {
    header = file('gradle/license-header.txt')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}

bintray {
    def bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    def bintrayApiKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    user = bintrayUser
    key = bintrayApiKey
    publications = ['BintrayPublication']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = bintrayUser
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/kaklakariada/fritzbox-java-api.git'
        version {
            name = project.version
            released  = new Date()
            vcsTag = project.version
        }
    }
}
